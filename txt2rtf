#! /usr/bin/perl
#
# This is a Perl script used to convert the Icarus Wing source to latex

my $speaker;
my $counter = 0;
my $in_quoted_block = 0;

sub ProcessLine {

    s/\$/\\\$/;				# quote $

    s/Christian1/Christian\$^1\$/;	# add superscripts to "Christian"
    s/Christians1/Christians\$^1\$/;
    s/Christian2/Christian\$^2\$/;
    s/Christians2/Christians\$^2\$/;

    s/^al-Nass:/alNass:/;		# handle the hyphen like this

    # The speaking character can be indicated by a notation at the
    # beginning of the line or paragraph like "Alister:".  Note
    # if this is the case, or undef $speaker if we're at a blank
    # line (in between paragraphs)

    $speaker=$1 if (/^((\w|-)+):/);
    undef $speaker if (/^$/);
    $counter=0 if (/^$/);

    # Remove speaker indication now that we've processed it.
    s/^(\w+)://;

    # Set text color for material in quotes according to speaker
    # Actual colors are defined above in the LaTeX preamble.

    while (/"/ and defined $speaker) {
	if ($counter % 2 == 0) {
	    my $speaker1 = $speaker;
	    if ($speaker =~ s/([^_]+)_(.*)/$2/) {
		$speaker1 = $1;
	    }
	    s/"/\\textcolor{$speaker1}{``/;
	} else {
	    s/"/''}/;
	}

	$counter ++;
    }

    while (/"/ and not /\\"/) {
	if ($counter % 2 == 0) {
	    s/"/``/;
	} else {
	    s/"/''/;
	}

	$counter ++;
    }

    s/_([-\w',!.?;]+)_/{\\it \1}/g;	# convert _ to italics
    s/_/ /g;				# convert interior _ to spaces

    # Change '    ' in chapter title lines to hskip
    s/    /\\hskip 0.5in/  if (/^T [+-] [0-9]+/);

    # Put chapter titles on new pages in typewriter font
    #   the centerline is there to make T + 1 day overfill
    s/^T [+-] [0-9]+.*$/\\vfill\\eject\\centerline{\\tt \\hfill $&}\\vskip 0.5in/;

    # Ditto for Launch Day
    s/    /\\hskip 0.5in/  if (/^Launch Day/);
    s/^Launch Day+.*$/\\vfill\\eject{\\tt \\hfill $&}\\vskip 0.5in/;

    # Ditto for Epilog
    s/    /\\hskip 0.5in/  if (/^Epilog/);
    s/^Epilog+.*$/\\vfill\\eject{\\tt \\hfill $&}\\vskip 0.5in/;

    # Format poetry at beginning - with italics {{ }}
    s/^{{([^}]*)}}$/\\centerline{\\it \1}/;

    # Format poetry at beginning - without italics {
    s/^{([^}]*)$/\\centerline{\1}/;

    # No math equations in rtf
    s|STOKES\.||;
    s|NEWTON\.||;

    # Indent quoted blocks
    if (/^   / and not $in_quoted_block) {
	print "\\begin{quote}\n";
	$in_quoted_block = 1;
    }
    if (not /^   / and not /^$/ and $in_quoted_block) {
	print "\\end{quote}\n";
	$in_quoted_block = 0;
    }

    if (/Requirements for The Great anti-American Novel/) {
	&insertTOC;
    } else {
	print;
    }
}

sub ProcessFile {
    my ($FILE) = @_;

    while (<$FILE>) {
	my $filename = $_;
	chomp $filename;

	if ($filename =~ m/^[-A-Z 0-9]*$/ and -r $filename) {
	    open(NESTED_FILE, "<$filename");
	    ProcessFile(\*NESTED_FILE);
	    close(NESTED_FILE);
	} else {
	    ProcessLine;
	}
    }
}

ProcessFile(\*STDIN);
